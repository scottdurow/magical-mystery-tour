// Prevent changing the listing associated
If(OldRecord.contoso_Listing.contoso_listingid <> NewRecord.contoso_Listing.contoso_listingid,
    Error(
        { 
            Kind: ErrorKind.Validation , 
            Message: $"The listing cannot be changed on an existing reservation." 
        });
);

// Validate that the reservation is not for a period that is already booked
If(OldRecord.contoso_from<>NewRecord.contoso_from || OldRecord.contoso_to<>NewRecord.contoso_to,
    If(
        // Ideally we would call the IsListingAvailable instant plugin here, but it's not supported because you can't import
        // a Power Fx Plugin that calls another Power Fx Plugin in the same solution
        CountRows(
            Filter(
                contoso_reservation As record, 
                // There is a bug currently when you can't import expressions that use global optionsets - it gives the error "Name isn't valid."
                // There is workaround to compare the optionset value as a string 
                // e.g. Text(record.'Reservation Status) <> "Cancelled"
                // but this is not delegable so we have to filter after the initial search
                //record.'Reservation Status' <> 'Reservation Status'.'Cancelled'
                //&&
                (
                    record.contoso_from < NewRecord.contoso_to && record.contoso_to > NewRecord.contoso_from
                )
                &&
                record.contoso_Listing.contoso_listingid1 = NewRecord.contoso_Listing.contoso_listingid1
                &&
                record.contoso_reservationid <> NewRecord.contoso_reservationid
            )
        )>0,
        Error(
            { 
                Kind: ErrorKind.Validation , 
                Message: $"There is already a reservation in this date range" 
            })
    );

    // Validate that the To is after the from date
    If(NewRecord.contoso_to<NewRecord.contoso_from,
        Error(
            { 
                Kind: ErrorKind.Validation ,
                Message: $"The To date must be after the From date" 
            })
    );
);


If(
    OldRecord.contoso_from<>NewRecord.contoso_from || OldRecord.contoso_to<>NewRecord.contoso_to,
    // Set the name if not already set
    Set(NewRecord.contoso_name,$"{NewRecord.contoso_Listing.contoso_displayname} - {NewRecord.contoso_from} - {NewRecord.contoso_to}");
);


